import stbl
use stbl

fun string() {
  println("## string")
  let mut tbl = stbl/create(2)
  stbl/insert(mut tbl, copy("key"), mov 1.1)
  fun find_println(tbl, key) {
    let v <- stbl/find(tbl, key)
    match v {
      Some(_v) -> println("1.1") -- TODO format float
      None -> println("none")
    }
  }
  find_println(tbl, "key")
  find_println(tbl, "otherkey")

  -- iter and enumerate
  use iter
  stbl/iter(tbl)
  |> enumerate
  |> iter(fun ((i, (key, value))) {
    fmt/(print3("it: {}, key: {}, value: {}\n", int, i, str, key, float, value))
  })

  stbl/remove(mut tbl, "key")
  find_println(tbl, "key")

}
println("# hashtbl")
string()

module key {
  type t = A1 | A2 | B1 | B2

  fun hash(k : t) { match k {
    A1 -> 0
    A2 -> 1
    B1 -> 2
    B2 -> 3
  }}
  fun equal(a, b) {hash(a) == hash(b)}
}
module keytbl = hashtbl/make(key)

fun key() {
  println("## key")
  let mut tbl = keytbl/create(512)
  keytbl/insert(mut tbl, mov A1, mov 10)
  keytbl/insert(mut tbl, mov A2, mov 20)
  let v <- keytbl/find(tbl, A1)
  match v {
    Some(v) -> fmt/(print1("some v: {}\n", int, v))
    None -> println("none")
  }
}
key()


fun mut_array() {
  println("## mut array")
  let mut tbl = keytbl/create(512)
  keytbl/insert(mut tbl, mov A1, mov [])

  {
    let mut arr <- keytbl/find_mut(mut tbl, A2)
    match arr {
      Some(_arr) -> assert(false)
      None -> ()
    }
  }
  {
    let mut arr <- keytbl/find_mut(mut tbl, A1)
    match mut arr {
      Some(mut arr) -> array/push(mut arr, 1)
      None -> assert(false)
    }
  }
  let v <- keytbl/find(tbl, A1)
  match v {
    Some(v) -> assert(array/length(v) == 1)
    None -> assert(false)
  }
}
mut_array()

fun project_mut() {
  println("## project mut")
  let mut tbl = keytbl/create(64)

  -- add from empty
  {
    let mut arr <- keytbl/find_mut(mut tbl, A1)
    match arr {
      None -> mut arr = Some([1, 2, 3])
      Some(_) -> assert(false)
    }
  }
  {
    let mut arr <- keytbl/find_mut(mut tbl, A1)
    match mut arr {
      None -> assert(false)
      Some(mut arr) -> mut arr = [4, 5]
    }
  }
  {
    let arr <- keytbl/find(tbl, A1)
    match arr {
      None -> assert(false)
      Some(arr) -> {
        assert(array/length(arr) == 2)
        assert(arr.[0] == 4)
        assert(arr.[1] == 5)
      }
    }
  }
  -- delete
  {
    let mut arr <- keytbl/find_mut(mut tbl, A1)
    match arr {
      None -> assert(false)
      Some(_) -> mut arr = None
    }
  }
  assert(keytbl/mem(tbl, A1) |> not)
}
project_mut()

-- regression test from schmorder issue. The size of the old data array is not
-- resetting correctly inside grow. Doesn't really reproduce. Still surfaced
-- another mono issue
{
  -- minimum hashtbl size will still be 16, hardcoded in hashtbl impl
  let mut tbl = stbl/create(2)
  -- add at least 16 items
  stbl/insert(mut tbl, copy("0"), copy("0"))
  stbl/insert(mut tbl, copy("1"), copy("1"))
  stbl/insert(mut tbl, copy("2"), copy("2"))
  stbl/insert(mut tbl, copy("3"), copy("3"))
  stbl/insert(mut tbl, copy("4"), copy("3"))
  stbl/insert(mut tbl, copy("5"), copy("3"))
  stbl/insert(mut tbl, copy("6"), copy("3"))
  stbl/insert(mut tbl, copy("7"), copy("3"))
  stbl/insert(mut tbl, copy("8"), copy("3"))
  stbl/insert(mut tbl, copy("9"), copy("3"))
  stbl/insert(mut tbl, copy("10"), copy("3"))
  stbl/insert(mut tbl, copy("10"), copy("4"))
  stbl/insert(mut tbl, copy("11"), copy("4"))
  stbl/insert(mut tbl, copy("12"), copy("3"))
  stbl/insert(mut tbl, copy("13"), copy("3"))
  stbl/insert(mut tbl, copy("14"), copy("3"))
  stbl/insert(mut tbl, copy("15"), copy("3"))
  stbl/insert(mut tbl, copy("16"), copy("3"))
  stbl/insert(mut tbl, copy("17"), copy("3"))

  -- now read all data. Some of it used to point to already freed memory
  stbl/iter(tbl) |> iter/iter(fun ((stra, strb)) {
    -- read items, but don't print
    string/len(stra) |> ignore
    string/len(strb) |> ignore
  })
}
