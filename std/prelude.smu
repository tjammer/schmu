-- string
(type cstr (raw_ptr u8))

(type string (array u8))

(external puts (fun cstr unit))

(fun print [str] (puts (array-data str)))

(fun string-len [str] (array-length str))

-- array
(fun array-iter [arr f]
  (fun inner [i]
    (if (= i (array-length arr))
      ()
      (do
        (f (array-get arr i))
        (inner (+ i 1)))))
  (inner 0))

(fun array-iteri [arr f]
  (fun inner [i]
    (if (= i (array-length arr))
      ()
      (do
        (f i (array-get arr i))
        (inner (+ i 1)))))
  (inner 0))

(fun array-fold [arr f init]
  (fun inner [i acc&]
    (if (= i (array-length arr))
      acc
      (do
        (set &acc (f acc (array-get arr i)))
        (inner (+ i 1) &acc))))
  (val acc& init)
  (inner 0 &acc))

(fun array-foldi [arr f init]
  (fun inner [i acc&]
    (if (= i (array-length arr))
      acc
      (do
        (set &acc (f i acc (array-get arr i)))
        (inner (+ i 1) &acc))))
  (val acc& init)
  (inner 0 &acc))

(fun array-map [arr f]
  (val ret& (__unsafe_array_create (array-length arr)))
  (set &(array-length ret) 0)
  -- TODO array-reserve
  (fun inner [i]
    (if (= i (array-length arr))
      ret
      (do
        (array-push &ret (f (array-get arr i)))
        (inner (+ i 1)))))
  (inner 0))

(fun iter-range [from to f]
  (fun inner [i]
    (if (= i to)
      ()
      (do (f i) (inner (+ i 1)))))
  (inner from))

-- option
(type (option 'a) ((#some 'a) #none))

-- char
(fun char-equal [a b] (= (u8_to_int a) (u8_to_int b)))
