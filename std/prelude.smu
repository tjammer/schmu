-- string
(type cstr (raw_ptr u8))

(type string (array u8))

(external puts (fun cstr unit))

(defn print [str] (puts (array-data str)))

(defn string-len [str] (array-length str))

-- array
(defn array-iter [arr f]
  (defn inner [i]
    (if (= i (array-length arr))
      ()
      (do
        (f (array-get arr i))
        (inner (+ i 1)))))
  (inner 0))

(defn array-iteri [arr f]
  (defn inner [i]
    (if (= i (array-length arr))
      ()
      (do
        (f i (array-get arr i))
        (inner (+ i 1)))))
  (inner 0))

(defn array-fold [arr f init]
  (defn inner [i acc&]
    (if (= i (array-length arr))
      acc
      (do
        (set &acc (f acc (array-get arr i)))
        (inner (+ i 1) &acc))))
  (def acc& init)
  (inner 0 &acc))

(defn array-foldi [arr f init]
  (defn inner [i acc&]
    (if (= i (array-length arr))
      acc
      (do
        (set &acc (f i acc (array-get arr i)))
        (inner (+ i 1) &acc))))
  (def acc& init)
  (inner 0 &acc))

(defn array-map [arr f]
  (def ret& (__unsafe_array_create (array-length arr)))
  (set &(array-length ret) 0)
  -- TODO array-reserve
  (defn inner [i]
    (if (= i (array-length arr))
      ret
      (do
        (array-push &ret (f (array-get arr i)))
        (inner (+ i 1)))))
  (inner 0))

(defn iter-range [from to f]
  (defn inner [i]
    (if (= i to)
      ()
      (do (f i) (inner (+ i 1)))))
  (inner from))

-- option
(type (option 'a) ((#some 'a) #none))

-- char
(defn char-equal [a b] (= (u8_to_int a) (u8_to_int b)))
