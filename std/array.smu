import iter
import option
import prelude

-- TODO write signature

let data = __array_data
let length = __array_length

use prelude

fun create(len) {
  let mut arr = __unsafe_array_create()
  mut __unsafe_array_length(arr) = 0
  if len > 0 {
    mut data(arr) = __unsafe_malloc(len)
    mut __unsafe_array_capacity(arr) = len
  }
  else {
    mut data(arr) = __unsafe_ptr_reinterpret(__unsafe_nullptr())
    mut __unsafe_array_capacity(arr) = 0
  }
  arr
}

fun init(len, mov value) {
  let mut arr = __unsafe_array_create()
  mut __unsafe_array_length(arr) = len
  if len > 0 {
    mut data(arr) = __unsafe_malloc(len)
    mut __unsafe_array_capacity(arr) = len
  }
  else {
    mut data(arr) = __unsafe_ptr_reinterpret(__unsafe_nullptr())
    mut __unsafe_array_capacity(arr) = 0
  }
  fun rec inner(i) {
    if i == len { () }
    else {
      -- a normal mutation would free an uninitialized value
      __unsafe_ptr_set(mut data(arr), i, copy(value))
      inner(i + 1)
    }
  }
  inner(0)
  arr
}

fun push(mut arr, mov value) {
  let capacity = __array_capacity(arr)
  let length = length(arr)

  if capacity == length {
    if capacity == 0 {
      assert(is_nullptr(data(arr)))
      mut data(arr) = __unsafe_malloc(4)
      mut __unsafe_array_capacity(arr) = 4
    } else {
      assert(is_nullptr(data(arr)) |> not)
      let cap = power_2_above_or_equal(capacity, capacity + 1)
      mut data(arr) = __unsafe_realloc(data(arr), cap)
      mut __unsafe_array_capacity(arr) = cap
    }
  }

  __unsafe_ptr_set(mut data(arr), length, mov value)
  mut __unsafe_array_length(arr) = length + 1
}

fun iter(arr, cont) {
  fun rec inner(i) {
    if i == length(arr) { false }
    else {
      if cont(arr.[i]) { inner(i + 1) }
      else { false }
    }
  }
  inner(0)
}

fun map(arr, f) {
  let ret = create(length(arr))
  fun rec inner(mov ret, i) {
    let mut ret = mov ret
    if i == length(arr) {
      ret
    } else {
      push(mut ret, mov f(arr.[i]))
      inner(mov ret, i + 1)
    }
  }

  inner(mov ret, 0)
}

fun map_inplace(mut arr, f) {
  fun rec inner(i) {
    if i == length(arr) {
      ()
    } else {
      f(mut arr.[i])
      inner(i + 1)
    }
  }

  inner(0)
}

fun pop_back(mut arr) {
  use option
  if length(arr) == 0 {
    None
  }
  else {
    Some(__unsafe_array_pop_back(mut arr))
  }
}

fun swap_items(mut arr, i, j) {
  if not (i == j) {
    let itmp = mov arr.[i]
    mut arr.[i] = arr.[j]
    mut arr.[j] = itmp
  }
}

fun clear(mut arr) {
  fun rec inner() {
    if 0 == length(arr) {
      ()
    } else {
      pop_back(mut arr) |> ignore
      inner()
    }
  }

  inner()
}

fun reserve(mut arr, size) {
  let cap = __array_capacity(arr)
  let startcap = if cap > 0 { cap } else { 1 }
  if cap < size {
    let newcap = power_2_above_or_equal(startcap, size)
    if is_nullptr(data(arr)) {
      mut data(arr) = __unsafe_malloc(newcap)
    }
    else {
      mut data(arr) = __unsafe_realloc(data(arr), newcap)
    }
    mut __unsafe_array_capacity(arr) = newcap
  }
}

fun reverse(mut arr) {
  let length = length(arr)
  iter_range(0, length / 2, fun i {
    swap_items(mut arr, i, length - i - 1)
  })
}

fun empty(arr) { length(arr) == 0 }

module fixed {
  let get = __fixed_array_get
  let length = __fixed_array_length
  let data = __fixed_array_data

  fun swap_items(mut arr, i, j) {
    if not (i == j) {
      let itmp = mov arr#[i]
      mut arr#[i] = arr#[j]
      mut arr#[j] = itmp
    }
  }

  fun reverse(mut arr) {
    let length = length(arr)
    iter_range(0, length / 2, fun i {
      swap_items(mut arr, i, length - i - 1)
    })
  }

}
