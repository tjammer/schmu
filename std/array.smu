import iter
import option
import prelude

let data = __array_data
let length = __array_length

use prelude

fun create(len) {
  let mut ret = __unsafe_array_create(len)
  mut __unsafe_array_length(ret) = 0
  ret
}

fun init(len, mov value) {
  let mut ret = __unsafe_array_create(len)
  mut __unsafe_array_length(ret) = len
  fun rec inner(i) {
    if i == len { () }
    else {
      mut ret.[i] = copy(value)
      inner(i + 1)
    }
  }
  inner(0)
  ret
}

fun push(mut arr, mov value) {
  let capacity = __array_capacity(arr)
  let length = length(arr)

  if capacity == length {
    if capacity == 0 {
      __unsafe_array_realloc(mut arr, 4)
    } else {
      __unsafe_array_realloc(mut arr, 2 * capacity)
    }
  }

  __unsafe_ptr_set(mut data(arr), length, mov value)
  mut __unsafe_array_length(arr) = length + 1
}

fun iter(arr, cont) {
  fun rec inner(i) {
    if i == length(arr) { false }
    else {
      if cont(arr.[i]) { inner(i + 1) }
      else { false }
    }
  }
  inner(0)
}

fun map(arr, f) {
  let ret = create(length(arr))
  fun rec inner(mov ret, i) {
    let mut ret = mov ret
    if i == length(arr) {
      ret
    } else {
      push(mut ret, mov f(arr.[i]))
      inner(mov ret, i + 1)
    }
  }

  inner(mov ret, 0)
}

fun map_inplace(mut arr, f) {
  fun rec inner(i) {
    if i == length(arr) {
      ()
    } else {
      f(mut arr.[i])
      inner(i + 1)
    }
  }

  inner(0)
}

fun pop_back(mut arr) {
  use option
  if length(arr) == 0 {
    None
  }
  else {
    Some(__unsafe_array_pop_back(mut arr))
  }
}

fun swap_items(mut arr, i, j) {
  if not (i == j) {
    let itmp = mov arr.[i]
    mut arr.[i] = arr.[j]
    mut arr.[j] = itmp
  }
}

fun clear(mut arr) {
  fun rec inner() {
    if 0 == length(arr) {
      ()
    } else {
      pop_back(mut arr) |> ignore
      inner()
    }
  }

  inner()
}

fun reserve(mut arr, size) {
  fun rec aux(newcap) {
    if newcap < size { aux(2 * newcap) }
    else { __unsafe_array_realloc(mut arr, newcap) }
  }
  let cap = __array_capacity(arr)
  if cap < size { aux(cap * 2) }
}

fun reverse(mut arr) {
  let length = length(arr)
  iter_range(0, length / 2, fun i {
    swap_items(mut arr, i, length - i - 1)
  })
}

fun empty(arr) { length(arr) == 0 }

module fixed {
  let get = __fixed_array_get
  let length = __fixed_array_length
  let data = __fixed_array_data

  fun swap_items(mut arr, i, j) {
    if not (i == j) {
      let itmp = mov arr#[i]
      mut arr#[i] = arr#[j]
      mut arr#[j] = itmp
    }
  }

  fun reverse(mut arr) {
    let length = length(arr)
    iter_range(0, length / 2, fun i {
      swap_items(mut arr, i, length - i - 1)
    })
  }

}
