import array
import iter
import option
import prelude
import string
import unsafe

signature {
  type t

  val open : fun (string/t) -> option/t[t]
  val close : fun (t) -> unit

  -- high level interface
  val with_open : fun (string/t, fun (mut t) -> 'a) -> option/t['a]
  val readall : fun (mut t) -> string/t
  val lines : fun (mut t, iter/consumer[string/t]) -> iter/continue
  val size : fun (t) -> int

  -- low level interface
  type buf = array[u8]
  val readbuf : fun (mut t, mut buf) -> option/t[int]
  val readn : fun (mut t, mut buf, int) -> option/t[int]
  val readline : fun (mut t, mut buf) -> option/t[int]
  val readrem : fun (mut t, mut buf) -> option/t[int]

  type file
  val of_file : fun (file) -> t
  val to_file : fun (t) -> file
}

type file = raw_ptr[u8]

type t = {mut buf : array#4096[u8], mut head : int, mut unseen : int, file : file}

type cstr = raw_ptr[u8]
external fread : fun (cstr, int, int, file) -> int
external fopen : fun (cstr, cstr) -> file
external fclose : fun (cstr) -> int

use option
use prelude

let bufsize = 4096

fun open(name) {
  let file = fopen(string/data(name), string/data("r"))
  if is_nullptr(file) {
    None
  } else {
    Some({buf = #4096[0u8], head = 0, unseen = 0, file})
  }
}

fun close(ic) {
  fclose(ic.file) |> ignore
}

fun with_open(name, f) {
  match open(name) {
    Some(ic) -> {
      let mut ic = mov ic
      let ret = f(mut ic)
      close(ic)
      Some(ret)
    }
    None -> None
  }
}

external memcpy : fun (raw_ptr[u8], raw_ptr[u8], int) -> unit

fun fill(mut buf, icbuf, lower, upper) {
  let diff = upper - lower
  let len = array/length(buf)
  -- We call [fill] often in [readall] so we allocate eagerly
  array/reserve(mut buf, prelude/power_2_above(16, len + diff))

  memcpy(array/data(buf) |> unsafe/at(len), array/fixed/data(icbuf) |> unsafe/at(lower), diff)
  mut __unsafe_array_length(buf) = len + diff
}

fun readbuf(mut ic, mut buf) {
  let lower, upper = {
    if ic.unseen > 0 {
      (ic.head, ic.head + ic.unseen)
    } else {
      (0, fread(__fixed_array_data(ic.buf), 1, bufsize, ic.file))
    }}
  -- copy content to buf
  fill(mut buf, ic.buf, lower, upper)
  mut ic.head = 0
  mut ic.unseen = 0
  Some(upper - lower)
}

fun rec readn_impl(mut ic, mut buf, n, readacc) {
  if ic.unseen >= n {
    fill(mut buf, ic.buf, ic.head, ic.head + n)
    mut ic.head = ic.head + n
    mut ic.unseen = ic.unseen - n
    Some(readacc + n)
  } else if ic.unseen > 0 {
    match readbuf(mut ic, mut buf) {
      Some(nread) -> {
        assert((n - nread) < 0)
        readn_impl(mut ic, mut buf, n - nread, nread + readacc)
      }
      None -> {
        assert(false)
        None
      }
    }
  } else {
    let nread = fread(__fixed_array_data(ic.buf), 1, bufsize, ic.file)
    mut ic.head = 0
    mut ic.unseen = copy(nread)
    if nread == 0 {
      if readacc == 0 { None } else { Some(readacc) }
    } else {
      readn_impl(mut ic, mut buf, n, readacc)
    }}
}

fun readn(mut ic, mut buf, n) { readn_impl(mut ic, mut buf, n, 0) }

fun rec readall_impl(mut ic, mut buf, acc) {
  -- assumes ic.unseen = 0
  let nread = fread(__fixed_array_data(ic.buf), 1, bufsize, ic.file)
  if nread == 0  {
    if acc == 0 { None } else { Some(acc) }
  } else {
    fill(mut buf, ic.buf, 0, nread)
    readall_impl(mut ic, mut buf, nread + acc)
  }
}

fun readrem(mut ic, mut buf) {
  let ret = match readbuf(mut ic, mut buf) {
    Some(nread) -> {
      fill(mut buf, ic.buf, ic.head, ic.head + ic.unseen)
      readall_impl(mut ic, mut buf, nread)
    }
    None -> None
  }
  -- make other (read) calls fail after this
  mut ic.unseen = 0
  mut ic.head = 0
  ret
}

fun readline(mut ic, mut buf) {
  fun rec find_newline(acc) {
    if ic.unseen > 0 {
      if prelude/char_equal('\n', ic.buf#[ic.head]) {
        mut ic.unseen = ic.unseen - 1
        mut ic.head = ic.head + 1
        Some(acc)
      } else {
        array/push(mut buf, mov copy(ic.buf#[ic.head]))
        mut ic.unseen = ic.unseen - 1
        mut ic.head = ic.head + 1
        find_newline(acc + 1)
      }
    } else {
      -- re-read file, then recurse, copied from readn_impl
      let nread = fread(__fixed_array_data(ic.buf), 1, bufsize, ic.file)
      mut ic.head = 0
      mut ic.unseen = copy(nread)
      if nread == 0 {
        if acc == 0 {
          None
        } else {
          Some(acc)
        }
      } else { find_newline(acc) }
    }}
  find_newline(0)
}

fun readall(mut ic) {
  let mut buf = array/create(bufsize)
  match readrem(mut ic, mut buf) {
    Some(_) -> {
      -- add null terminator
      array/push(mut buf, '\000')
      string/of_array(mov buf)
    }
    None -> copy("")
  }
}

fun lines(mut ic, cont) {
  fun rec aux(mov buf) {
    let mut buf = mov buf
    match readline(mut ic, mut buf) {
      Some(_) -> {
        let str = string/of_array(mov buf)
        if cont(str) {
          mut buf = string/to_array(mov str)
          array/clear(mut buf)
          aux(buf)
        }
        else { false }
      }
      None -> false
    }
  }
  let buf = array/create(bufsize)
  aux(buf)
}

external rewind : fun (file) -> unit
fun of_file(file) {
  rewind(file)
  {buf = #4096[0u8], head = 0, unseen = 0, file}
}

fun to_file(ic) { ic.file }

external fseek : fun (file, int, i32) -> unit
external ftell : fun (file) -> int
let seek_set = 0i32
let seek_end = 2i32
fun size(ic) {
  let curr = ftell(ic.file)
  fseek(ic.file, 0, seek_end)
  let end = ftell(ic.file)
  fseek(ic.file, curr, seek_set)
  end
}
