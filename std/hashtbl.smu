import array
import option
import prelude
import unsafe

module type key {
  type t
  val hash : fun (t) -> int
  val equal : fun (t, t) -> bool
}

module type sig {
  type key
  type t['value]

  val create : fun (int) -> t['value]
  val length : fun (t['value]) -> int
  val insert : fun (mut t['value], mov key, mov 'value) -> unit
  val remove : fun (mut t['value], key) -> unit

  val find : fun (t['value], key, once fun (option/t['value]) -> 'a) -> 'a
  val find_mut : fun (mut t['value], key, once fun (mut option/t['value]) -> 'a) -> 'a
  val mem : fun (t['value], key) -> bool

  val fold : fun (t['value], mov 'b, fun (mov 'b, key, 'value) -> 'c) -> 'c
  val clear : fun (mut t['value]) -> unit
  val to_array : fun (t['value]) -> array[(key, 'value)]
}

functor make : sig(m : key) {
  use option
  use prelude

  type key = m/t
  type item['a] = {key : m/t, mut value : 'a, mut pathlen : int}
  type slot['a] = option/t[item['a]]
  type t['a] = {mut data : array[slot['a]], mut nitems : int}

  fun create(size) {
    let size = power_2_above(16, size)
    let mut data = __unsafe_array_create(size)
    prelude/iter_range(0, size, fun i {
      __unsafe_ptr_set(mut array/data(data), i, mov None)
    })
    {data, nitems = 0}
  }

  fun length(tbl) { tbl.nitems }

  fun idx(tbl, key) {
    let hash = m/hash(key)
    land(hash, array/length(tbl.data) - 1)
  }

  fun next_wrapped(curr, size) {
    if (curr + 1) == size { 0 } else { curr + 1 }
  }

  fun rec redist(mut tbl, mov item, curr, pathlen) {
    let size = array/length(tbl.data)
    match tbl.data.[curr] {
      None -> {
        let mut item = mov item
        mut item.pathlen = pathlen
        mut tbl.data.[curr] = Some(item)
      }
      Some(other) -> {
        let next = next_wrapped(curr, size)
        if pathlen > other.pathlen {
          let other_pathlen = copy(other.pathlen)
          let new_item = mov other
          let mut item = mov item
          mut item.pathlen = pathlen
          mut tbl.data.[curr] = Some(item)
          redist(mut tbl, mov new_item, next, other_pathlen + 1)
        }
        else {
          redist(mut tbl, mov item, next, pathlen + 1)
        }
      }
    }
  }

  let load_limit = 0.75

  fun load_factor(tbl) {
    float_of_int(tbl.nitems) /. float_of_int(array/length(tbl.data))
  }

  fun iter_data_move(mut data, f) {
    fun rec inner(i) {
      if i == array/length(data) {
        mut __unsafe_array_length(data) = 0
      }
      else {
        match __unsafe_ptr_get(array/data(data), i) {
          None -> inner(i + 1)
          Some({key, value, pathlen = _}) -> {
            f(mov key, mov value)
            inner(i + 1)
          }
        }
      }
    }
    inner(0)
  }

  fun rec grow(mut tbl) {
    let size = 2 * array/length(tbl.data)
    let mut data = __unsafe_array_create(size)
    prelude/iter_range(0, size, fun i {
      __unsafe_ptr_set(mut array/data(data), i, mov None)
    })
    let mut old_data = mov tbl.data
    mut tbl.data = data
    mut tbl.nitems = 0
    iter_data_move(mut old_data, fun(mov key, mov value) {
      insert(mut tbl, mov key, mov value)
    })
  }

  and insert(mut tbl, mov key, mov value) {
    if load_factor(tbl) >. load_limit { grow(mut tbl) }
    let hash = idx(tbl, key)
    let size = array/length(tbl.data)

    fun rec insert(i, pathlen, mov key, mov value) {
      match tbl.data.[i] {
        None -> {
          mut tbl.data.[i] = Some({key, value, pathlen})
          mut tbl.nitems = tbl.nitems + 1
        }
        Some(other) -> {
          if m/equal(key, other.key) {
            -- overwrite
            mut tbl.data.[i] = Some({key, value, pathlen})
          }
          else {
            let next = next_wrapped(i, size)
            if pathlen > other.pathlen {
              let other_pathlen = copy(other.pathlen)
              -- probe sequence length of new value is larger. robin hood
              let other_item = mov other
              mut tbl.data.[i] = Some({key, value, pathlen})
              mut tbl.nitems = tbl.nitems + 1
              redist(mut tbl, mov other_item, next, other_pathlen + 1)
            }
            else {
              insert(next, pathlen + 1, mov key, mov value)
            }
          }
        }
      }
    }
    insert(hash, 0, mov key, mov value)
  }

  fun rec fixup(mut data, old) {
    let next = next_wrapped(old, array/length(data))
    match mut data.[next] {
      Some(mut item) ->
        if not(item.pathlen == 0) {
          mut item.pathlen = item.pathlen - 1
          -- swap with old slot, inline
          let oldtmp = mov data.[old]
          mut data.[old] = data.[next]
          mut data.[next] = oldtmp
          fixup(mut data, next)
        }
      None -> ()
    }
  }

  fun rec find_index(i, tbl, key, pathlen) {
    match tbl.data.[i] {
      Some(item) ->
        if m/equal(key, item.key) { Some(i) }
        else {
          if pathlen > item.pathlen { None }
          else {
            let next = next_wrapped(i, array/length(tbl.data))
            find_index(next, tbl, key, pathlen + 1)
          }
        }
      None -> None
    }
  }


  fun remove(mut tbl, key) {
    let hash = idx(tbl, key)
    match find_index(hash, tbl, key, 0) {
      Some(i) -> {
        mut tbl.data.[i] = None
        mut tbl.nitems = tbl.nitems - 1
        fixup(mut tbl.data, i)
      }
      None -> ()
    }
  }

  fun find(tbl, key, once fn) {
    let hash = idx(tbl, key)
    match find_index(hash, tbl, key, 0) {
      Some(i) -> match tbl.data.[i] {
        Some(item) -> {
          let tmp = unsafe/unchecked(Some(item.value))
          let ret = fn(tmp)
          unsafe/leak(tmp)
          ret
        }
        None -> fn(None)
      }
      None -> fn(None)
    }
  }

  fun find_mut(mut tbl, key, once fn) {
    let hash = idx(tbl, key)
    match find_index(hash, tbl, key, 0) {
      Some(i) -> match mut tbl.data.[i] {
        Some(mut item) -> {
          let mut tmp = Some(item.value)
          let ret = fn(mut tmp)
          -- our value could have been deleted or changed
          match tmp {
            Some(value) -> mut item.value = value
            None -> {
              -- partially copied from [remove]
              mut tbl.data.[i] = None
              mut tbl.nitems = tbl.nitems - 1
              fixup(mut tbl.data, i)
            }
          }
          ret
        }
        None -> {
          let mut tmp = None
          let ret = fn(mut tmp)
          match tmp {
            None -> ()
            Some(value) -> insert(mut tbl, copy(key), value)
          }
          ret
        }
      }
      None -> {
          let mut tmp = None
          let ret = fn(mut tmp)
          match tmp {
            None -> ()
            Some(value) -> insert(mut tbl, copy(key), value)
          }
          ret
        }
    }
  }

  fun mem(tbl, key) {
    let hash = idx(tbl, key)
    match find_index(hash, tbl, key, 0) {
      Some(_) -> true
      None -> false
    }
  }

  fun fold(tbl, mov init, f) {
    fun rec inner(i, mov acc) {
      if array/length(tbl.data) == i {
        acc
      }
      else {
        match tbl.data.[i] {
          None -> inner(i + 1, mov acc)
          Some({key, value, pathlen = _}) ->
          inner(i + 1, mov f(mov acc, key, value))
        }
      }
    }
    inner(0, mov init)
  }

  fun clear(mut tbl) {
    array/clear(mut tbl.data)
    mut tbl.nitems = 0
  }

  fun iter_data(data, f) {
    fun rec inner(i) {
      if array/length(data) == i { () }
      else {
        match data.[i] {
          None -> inner(i + 1)
          Some({key, value, pathlen = _}) -> {
            f(key, value)
            inner(i + 1)
          }
        }
      }
    }
    inner(0)
  }

  fun to_array(tbl) {
    let mut arr = array/create(tbl.nitems)
    iter_data(tbl.data, fun(key, value) {
      array/push(mut arr, mov (copy(key), copy(value)))
    })
    arr
  }

}
